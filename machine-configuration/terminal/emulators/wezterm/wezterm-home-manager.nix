{
  lib,
  pkgs,
  inputs,
  isNixOS,
  latest,
  ...
}:
let
  nixglWrap = import ../../../../repository/nix-library/nixgl-wrap.nix {
    inherit pkgs inputs isNixOS;
  };

  patchedWezterm = import ./patched-wezterm.nix { inherit latest; };

  weztermAfterNixGL = nixglWrap.wrapWithNixGLIntel {
    package = patchedWezterm;
    binaries = [
      "wezterm"
      "wezterm-gui"
    ];
  };

  weztermBundledBinariesForDarwinAppLaunchers = pkgs.symlinkJoin {
    name = "wezterm-darwin-app-bundle";
    paths = [ weztermAfterNixGL ];
    postBuild = ''
      darwinAppBundleContentsMacOS="$out/Applications/WezTerm.app/Contents/MacOS"
      mkdir -p "$darwinAppBundleContentsMacOS"
      for darwinAppBundleExecutable in wezterm wezterm-gui wezterm-mux-server strip-ansi-escapes wezterm.sh; do
        ln -s "${patchedWezterm}/Applications/WezTerm.app/$darwinAppBundleExecutable" \
          "$darwinAppBundleContentsMacOS/$darwinAppBundleExecutable"
      done
    '';
  };

  weztermPackage =
    if pkgs.stdenv.hostPlatform.isDarwin then
      weztermBundledBinariesForDarwinAppLaunchers
    else
      weztermAfterNixGL;
in
{
  home.file.".config/wezterm/wallpaper.png".source =
    ../../../desktop/appearance/theming/wallpapers/artwork/wallpaper.png;

  xdg.configFile."xdg-terminals.list" = lib.mkIf isNixOS { source = ./xdg-terminals.list; };

  programs.wezterm = {
    enable = true;
    package = weztermPackage;
    extraConfig = builtins.readFile ./program-configuration/wezterm.lua;
  };
}
